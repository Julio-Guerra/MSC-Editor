# set the default ANTLR_ROOT value
IF(NOT ANTLR_ROOT)
  SET(ANTLR_ROOT $ENV{ANTLR_ROOT} CACHE PATH "The ANTLR directory root.")
ENDIF(NOT ANTLR_ROOT)

FIND_FILE(ANTLR_CLASSPATH
  NAMES
    antlr-3.1.1.jar
    antlr-3.1.2.jar
    antlr-3.1.3.jar
  PATHS
    ${ANTLR_ROOT}/lib
  DOC "The ANTLR class file")

FIND_LIBRARY(ANTLR_LIBRARY
  NAMES antlr3c
  PATHS
    ${ANTLR_ROOT}/runtime/C
    ${ANTLR_ROOT}/runtime/C/Release
  DOC "The ANTLR library")

FIND_LIBRARY(ANTLR_LIBRARY_DEBUG
  NAMES antlr3cd
  PATHS
    ${ANTLR_ROOT}/runtime/C
    ${ANTLR_ROOT}/runtime/C/Debug
  DOC "The ANTLR debug library")

IF(NOT ANTLR_LIBRARY_DEBUG)
  SET(ANTLR_LIBRARY_DEBUG ${ANTLR_LIBRARY})
  SET(ANTLR_LIBRARIES ${ANTLR_LIBRARY})
ELSE()
  SET(ANTLR_LIBRARIES 
    optimized ${ANTLR_LIBRARY}
    debug ${ANTLR_LIBRARY_DEBUG}
  )
ENDIF()

FIND_PATH(ANTLR_INCLUDE_DIR
  NAMES antlr3.h
  PATHS
    ${ANTLR_ROOT}/runtime/C/include
  DOC "The ANTLR include files")

INCLUDE(FindPackageHandleStandardArgs)
FIND_PACKAGE_HANDLE_STANDARD_ARGS(ANTLR DEFAULT_MSG
  ANTLR_CLASSPATH ANTLR_LIBRARY ANTLR_INCLUDE_DIR)
